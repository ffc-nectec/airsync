import java.nio.charset.StandardCharsets

/*
 * Copyright (c) 2018 NECTEC
 *   National Electronics and Computer Technology Center, Thailand
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

group 'ffc.airsync'

version = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    String tag = stdout.toString().trim();
    if (tag.length() == 0)
        tag = "0.0.0"
    println "version $tag"
    return tag
}.call()

buildscript {
    ext.kotlin_version = '1.3.10'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

ext {
    entitiesVersion = '1.0.0'
}

allprojects {
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://dl.bintray.com/blazei/maven" }
    }
}

subprojects {
    apply plugin: 'kotlin'

    sourceCompatibility = 1.8
    compileKotlin { kotlinOptions.jvmTarget = "1.8" }
    compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }

    dependencies {
        compile "com.github.ffc-nectec:entities:$entitiesVersion"
        compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1'
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

        testImplementation "junit:junit:4.12"
        testImplementation "org.amshove.kluent:kluent:1.34"
    }

    configurations { ktlint }
    dependencies { ktlint "com.github.shyiko:ktlint:0.24.0" }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.github.shyiko.ktlint.Main"
        args "src/**/*.kt"
    }
    check.dependsOn ktlint

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.github.shyiko.ktlint.Main"
        args "-F", "src/**/*.kt"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8'
    distributionUrl = distributionUrl.replace("bin", "all")
}