/*
 * Copyright (c) 2018 NECTEC
 *   National Electronics and Computer Technology Center, Thailand
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.palantir.git-version' version '0.11.0'
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
}

group 'ffc.airsync'
version = gitVersion()

ext {
    entitiesVersion = '1.1.0'
    jdbiVersion = "3.1.0"
    h2Version = '1.4.197'
}

allprojects {
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://dl.bintray.com/blazei/maven" }
    }
}

subprojects {
    version = rootProject.version
    group = rootProject.group

    apply plugin: 'kotlin'

    sourceCompatibility = 1.8
    compileKotlin { kotlinOptions.jvmTarget = "1.8" }
    compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }

    dependencies {
        compile "com.github.ffc-nectec:entities:1.1.1a2"
        compile "com.github.lionants02:kotlin-util:0.3"
        compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1'
        compile 'org.apache.logging.log4j:log4j-core:2.11.1'
        compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.1'
        compile 'org.apache.logging.log4j:log4j-api-kotlin:1.0.0'

        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.21"
        testImplementation "junit:junit:4.12"
        testImplementation "org.amshove.kluent:kluent:1.34"
        testImplementation "com.wix:wix-embedded-mysql:4.6.1"
        testImplementation "ch.vorburger.mariaDB4j:mariaDB4j:2.4.0"
    }

    configurations { ktlint }
    dependencies { ktlint "com.github.shyiko:ktlint:0.31.0" }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.github.shyiko.ktlint.Main"
        args "src/**/*.kt"
    }
    check.dependsOn ktlint

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.github.shyiko.ktlint.Main"
        args "-F", "src/**/*.kt"
    }
}

